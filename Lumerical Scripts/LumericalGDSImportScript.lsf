switchtolayout;
selectall;
delete;

mode = '3D';
target = 'kernel3';

## SIM PARAMS

WLDesign = 1.525e-6;
WLMin = 1.400e-6;
WLMax = 1.600e-6;
WLNPoints = 201;

siHeight = 220.e-9;
siEtch1Height = 220.e-9 - 70.e-9;
siEtch2Height = 220.e-9 - 130.e-9;

siIndex = 3.47885;
siO2Index = 1.44433;
siWGIndex = 2.864526;
etch1WGIndex = 2.556470;
etch2WGIndex = 2.118832;
siO2WGIndex = 1.44433;

if(target == 'kernel1'){
    GDSCellName = 'kernel1';
    GDSFilePath = '.\kernel1.gds';
    nInputs = 3;
    nOutputs = 3;
    portYSep =  2.745e-6;
    portXSep = 40.676e-6;
    x_orig = -portXSep/2;
    y_orig = 0;
    kHeight = 14e-6;
} else if(target == 'kernel2'){
    GDSCellName = 'kernel4';
    GDSFilePath = '.\kernel4.gds';
    nInputs = 3;
    nOutputs = 3;
    portYSep =  2.745e-6;
    portXSep = 40.676e-6;
    x_orig = -portXSep/2;
    y_orig = 0;
    kHeight = 14e-6;
} else if(target == 'kernel3'){
    GDSCellName = 'kernel3';
    GDSFilePath = '.\kernel3.gds';
    nInputs = 3;
    nOutputs = 3;
    portYSep = 11.000e-6;
    portXSep = 50.476e-6;
    x_orig = -portXSep/2;
    y_orig = 0;
    kHeight = 30e-6;
} else if(target == 'kernel4'){
    GDSCellName = 'kernel2';
    GDSFilePath = '.\kernel2.gds';
    nInputs = 2;
    nOutputs = 2;
    portYSep = 2.745e-6;
    portXSep = 22.676e-6;
    x_orig = -portXSep/2 - 0.1e-6;
    y_orig = 0.0775e-6;
    kHeight = 14e-6;
} else if(target == 'cal1'){
    GDSCellName = 'Cal1';
    GDSFilePath = '.\cal1.gds';
    nInputs = 1;
    nOutputs = 1;
    portYSep = 4e-6;
    portXSep = 40.676e-6;
    x_orig = 0;
    y_orig = 0;
    kHeight = 10e-6;
} else if(target == 'cal2'){
    GDSCellName = 'Cal4';
    GDSFilePath = '.\cal4.gds';
    nInputs = 1;
    nOutputs = 1;
    portYSep = 4e-6;
    portXSep = 40.676e-6;
    x_orig = 0;
    y_orig = 0;
    kHeight = 10e-6;
} else if(target == 'cal3'){
    GDSCellName = 'Cal1';
    GDSFilePath = '.\cal1.gds';
    nInputs = 1;
    nOutputs = 1;
    portYSep = 4e-6;
    portXSep = 50.476e-6;
    x_orig = 0;
    y_orig = 0;
    kHeight = 10e-6;
} else if(target == 'cal4'){
    GDSCellName = 'Cal2';
    GDSFilePath = '.\cal4.gds';
    nInputs = 1;
    nOutputs = 1;
    portYSep = 4e-6;
    portXSep = 22.676e-6;
    x_orig = 0;
    y_orig = 0;
    kHeight = 10e-6;
}

marg = 0.5e-6;

GDSSiLayerNumber = 1;
GDSEtch1LayerNumber = 2;
GDSEtch2LayerNumber = 3;

wgWidth = 500e-9; 
tailLength = 5e-6;

## Build Simulation

setglobalsource("wavelength start", WLMin);
setglobalsource("wavelength stop", WLMax);
setglobalmonitor("frequency points", WLNPoints);


portWidth = 2*portYSep - 3*wgWidth;

size_x = portXSep + 2*marg;
size_y = kHeight + 2*marg;
size_z = WLMax;

x_off = 0;
y_off = 0;
z_off = 0;

x_min = -size_x/2 + x_off;
x_max =  size_x/2 + x_off;
y_min = -size_y/2 + y_off;
y_max =  size_y/2 + y_off;
z_min =  siHeight/2 - WLMax/2;
z_max =  siHeight/2 + WLMax/2;

x_inputPlane = -portXSep/2 + x_off;
x_outputPlane = portXSep/2 + x_off;


## Design Import
# Note that the etch layers need to be
# imported first so that the Si layer
# will take precedence.

    # Import Etch1 Layer
if(mode == '3D'){          
    gdsimport(GDSFilePath,
              GDSCellName,
              GDSEtch1LayerNumber,
              '<Object defined dielectric>',
              0.0,
              siEtch1Height);
    set("index", siIndex);
    set("name", "Etch1Import");
    set("x", x_orig);
    set("y", y_orig);
} else if (mode == '2D'){
    gdsimport(GDSFilePath,
              GDSCellName,
              GDSEtch1LayerNumber,
              '<Object defined dielectric>',
              0.0,
              siHeight);
    set("index", etch1WGIndex);
    set("name", "Etch1Import");
    set("x", x_orig);
    set("y", y_orig);
}

    # Import Etch2 Layer
if(mode == '3D'){          
    gdsimport(GDSFilePath,
              GDSCellName,
              GDSEtch2LayerNumber,
              '<Object defined dielectric>',
              0.0,
              siEtch1Height);
    set("index", siIndex);
    set("name", "Etch2Import");
    set("x", x_orig);
    set("y", y_orig);
} else if (mode == '2D'){
    gdsimport(GDSFilePath,
              GDSCellName,
              GDSEtch2LayerNumber,
              '<Object defined dielectric>',
              0.0,
              siHeight);
    set("index", etch2WGIndex);
    set("name", "Etch2Import");
    set("x", x_orig);
    set("y", y_orig);
}

    # Import Si Layer
if(mode == '3D'){
    gdsimport(GDSFilePath,
          GDSCellName,
          GDSSiLayerNumber,
          '<Object defined dielectric>',
          0.0,
          siHeight);
    set("index", siIndex);
    set("name", "SiImport");
    set("x", x_orig);
    set("y", y_orig);
}
else if (mode == '2D'){
    gdsimport(GDSFilePath,
          GDSCellName,
          GDSSiLayerNumber,
          '<Object defined dielectric>',
          0.0,
          siHeight);
    set("index", siWGIndex);
    set("name", "SiImport");
    set("x", x_orig);
    set("y", y_orig);
}

# Build WG Tails to smoothly go into PML.

if(mode=='3D'){
    tailIndex = siIndex;
} else if (mode=='2D'){
    tailIndex = siWGIndex;
}
for(xi=0:1){
    if(xi==0){
        tailXMin = -portXSep/2 - 5e-6;
        tailXMax = -portXSep/2;
    }
    else if(xi==1){
        tailXMin = portXSep/2;
        tailXMax = portXSep/2 + 5e-6;
    }
    for(yi=0:nInputs-1){
        addrect;
        set("name","WGTails");
        set("x min", tailXMin);
        set("x max", tailXMax);
        set("y", -portYSep*(nInputs-1)/2 + yi*portYSep);
        set("y span", 0.5e-6);
        set("z min",0);
        set("z max", siHeight);
        set("index", siIndex);
        #set("material","Si (Silicon) - Palik");
    }
}



## FDTD

if(mode == '2D'){
    addvarfdtd;
    set('dimension','2D');
} else if (mode == '3D') {
    addfdtd;
    set('dimension','3D');
}
set('background index', siO2Index);
set('mesh accuracy',4);
set('z', siHeight/2);
set('x min', x_min);
set('x max', x_max);
set('y min', y_min);
set('y max', y_max);
if (mode == '3D') {
    set('z min', z_min);
    set('z max', z_max);
}

## PORTS

for(i = 1:nInputs) 
{
    yCenter = -portYSep*(i-(nInputs+1.)/2);
    iStr = num2str(i);
    addport;
        set('name','in'+iStr);
        set('x', x_inputPlane);
        set('y min',yCenter - portWidth/2);
        set('y max',yCenter + portWidth/2);
        set('z min', z_min);
        set('z max', z_max);    
}

for(i = 1:nOutputs) 
{
    yCenter = -portYSep*(i-(nOutputs+1.)/2);
    iStr = num2str(i);
    addport;
        set('name','out'+iStr);
        set('direction', 'backward');
        set('x', x_outputPlane);
        set('y min',yCenter - portWidth/2);
        set('y max',yCenter + portWidth/2);
        set('z min', z_min);
        set('z max', z_max);    
}

## MONITORS

addindex;
    set('name','IndexMonitor');
    set('x min', x_min);
    set('x max', x_max);	
    set('y min', y_min);
    set('y max', y_max);
    set('z min', z_min);
    set('z max', z_max);

addpower;
    set('name','opt_fields');
    set('monitor type','2D Z-normal');
    set('z', 110e-9);
    set('x min', x_min);
    set('x max', x_max);
    set('y min', y_min);
    set('y max', y_max);
    set('override global monitor settings', 1);
    set('frequency points', 1);
    set('use source limits', 0);
    set('wavelength center', WLDesign);

if(mode == '3D'){
    addpower;
        set('name','opt_fields3D');
        set('monitor type','3D');
        set('x min', x_min);
        set('x max', x_max);	
        set('y min', y_min);
        set('y max', y_max);
        set('z min', z_min);
        set('z max', z_max);
        set('override global monitor settings', 1);
        set('frequency points', 1);
        set('use source limits', 0);
        set('wavelength center', WLDesign);
}



    



